dependencies {
    compile project(':common:rest-java-model')
    compile project(':common:transform')

    compile group: 'ai.h2o', name: 'mojo2-runtime-api'
    compile group: 'ai.h2o', name: 'mojo2-runtime-impl'
    compile group: 'com.amazonaws', name: 'aws-lambda-java-core'
    compile group: 'com.amazonaws', name: 'aws-lambda-java-events'
    compile group: 'com.amazonaws', name: 'aws-java-sdk-s3'
    compile group: 'com.google.code.gson', name: 'gson'
    compile group: 'io.swagger.core.v3', name: 'swagger-annotations'

    testCompile group: 'com.google.truth.extensions', name: 'truth-java8-extension'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

// Build an the AWS lambda distribution zip file.
task buildZip(type: Zip) {
    archiveName "${project.name}.zip"
    from compileJava
    from processResources
    into('lib') {
        from configurations.compileClasspath
    }
}
build.dependsOn buildZip

// Include the AWS lambda distribution zip file in the root package distribution zip file.
rootProject.distributionZip {
    dependsOn buildZip
    into(this.project.path.replace(":", "/")) {
        from buildZip.archivePath
    }
}

