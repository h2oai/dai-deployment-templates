apply plugin: 'java'
apply plugin: 'org.hidetake.swagger.generator'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile group: 'ai.h2o', name: 'mojo2-runtime-api', version: mojoRuntimeVersion
    compile group: 'ai.h2o', name: 'mojo2-runtime-impl', version: mojoRuntimeVersion
    compile group: 'com.amazonaws', name: 'aws-lambda-java-core', version: awsLambdaCoreVersion
    compile group: 'com.amazonaws', name: 'aws-lambda-java-events', version: awsLambdaEventsVersion
    compile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: awsSdkS3Version
    compile group: 'com.google.code.gson', name: 'gson', version: gsonVersion
    compile group: 'io.swagger.core.v3', name: 'swagger-annotations', version: swaggerCoreVersion
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.0'

    testCompile group: 'com.google.truth.extensions', name: 'truth-java8-extension', version: truthVersion
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jupiterVersion
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jupiterVersion
}

test {
    useJUnitPlatform()
}

swaggerSources {
    scorer {
        inputFile = file('../../common/rest-api/swagger.yaml')
        code {
            language = 'java'
            configFile = file('swagger_codegen.json')
            components = [models: true]
            dependsOn validation
        }
    }
}
compileJava.dependsOn swaggerSources.scorer.code
sourceSets.main.java.srcDir "${swaggerSources.scorer.code.outputDir}/src/main/java"

// Build an the AWS lambda distribution zip file.
task buildZip(type: Zip) {
    archiveName "${project.name}.zip"
    from compileJava
    from processResources
    into('lib') {
        from configurations.compileClasspath
    }
}
build.dependsOn buildZip

// Include the AWS lambda distribution zip file in the root package distribution zip file.
rootProject.distributionZip {
    dependsOn buildZip
    into(this.project.path.replace(":", "/")) {
        from buildZip.archivePath
    }
}

