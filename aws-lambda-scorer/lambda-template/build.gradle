apply from: project(":").file('gradle/java.gradle')

dependencies {
    implementation project(':common:rest-java-model')
    implementation project(':common:transform')
    implementation group: 'ai.h2o', name: 'mojo2-runtime-api'
    implementation group: 'ai.h2o', name: 'mojo2-runtime-impl'
    implementation group: 'com.amazonaws', name: 'aws-lambda-java-core'
    implementation group: 'com.amazonaws', name: 'aws-lambda-java-events'
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-s3'
    implementation group: 'com.google.code.gson', name: 'gson'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations'

    testImplementation group: 'com.google.truth.extensions', name: 'truth-java8-extension'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

// Build an the AWS lambda distribution zip file.
task buildZip(type: Zip) {
    archiveName "${project.name}.zip"
    from compileJava
    from processResources
    into('lib') {
        from configurations.compileClasspath
    }
}
build.dependsOn buildZip

// Include the AWS lambda distribution zip file in the root package distribution zip file.
rootProject.distributionZip {
    dependsOn buildZip
    into(this.project.path.replace(":", "/")) {
        from buildZip.archivePath
    }
}
