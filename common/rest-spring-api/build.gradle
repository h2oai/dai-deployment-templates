plugins {
    id 'org.springframework.boot'
    id 'org.hidetake.swagger.generator'
}
apply from: project(":").file('gradle/java.gradle')

dependencies {
    implementation group: 'com.squareup.okhttp', name: 'okhttp'
    implementation group: 'com.squareup.okhttp', name: 'logging-interceptor'
    implementation group: 'com.google.code.gson', name: 'gson'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    swaggerCodegen group: 'io.swagger.codegen.v3', name: 'swagger-codegen-cli'
}

swaggerSources {
    api {
        inputFile = file('../swagger/v1/swagger.yaml')
        code {
            language = 'spring'
            configFile = file('../swagger/v1/swagger_codegen.json')
            components = [models: true, apis: true]
            dependsOn validation
        }
    }
    apiV2 {
        inputFile = file("../swagger/v2/swagger.yaml")
        code {
            language = 'spring'
            configFile = file('../swagger/v2/swagger_codegen.json')
            components = ['models': true, 'apis': true]
            dependsOn validation
        }
    }
}

jar {
    // As a SpringBoot module, we need to enable the jar file generation.
    enabled=true
}

bootJar {
    enabled=false
}

sourceSets {
    main {
        java {
            srcDirs "${swaggerSources.api.code.outputDir}/src/main/java"
            srcDirs "${swaggerSources.apiV2.code.outputDir}/src/main/java"
        }
    }
}

compileJava.dependsOn swaggerSources.api.code
