plugins {
    id 'org.springframework.boot'
    id 'org.hidetake.swagger.generator'
}
apply from: project(":").file('gradle/java.gradle')

dependencies {
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    swaggerCodegen group: 'io.swagger.codegen.v3', name: 'swagger-codegen-cli'
}

swaggerSources {
    api {
        inputFile = file('../swagger/v1/swagger.yaml')
        code {
            language = 'spring'
            configFile = file('../swagger/v1/swagger_codegen.json')
            components = [models: true, apis: true]
            dependsOn validation
        }
    }
    apiV1Ext {
        inputFile = file('../swagger/v1ext/swagger.yaml')
        code {
            language = 'spring'
            configFile = file('../swagger/v1ext/swagger_codegen.json')
            components = [models: true, apis: true]
            dependsOn validation
        }
    }
}

jar {
    // As a SpringBoot module, we need to enable the jar file generation.
    enabled=true
}

bootJar {
    enabled=false
}

sourceSets {
    main {
        java {
            srcDirs "${swaggerSources.api.code.outputDir}/src/main/java"
            srcDirs "${swaggerSources.apiV1Ext.code.outputDir}/src/main/java"
        }
    }
}

compileJava {
    dependsOn swaggerSources.api.code
    dependsOn swaggerSources.apiV1Ext.code
}
