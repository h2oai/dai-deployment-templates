plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'com.google.cloud.tools.jib'
}
apply from: project(":").file('gradle/java_no_style.gradle')

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation group: 'ai.h2o', name: 'mojo2-runtime-api'
    implementation group: 'ai.h2o', name: 'mojo2-runtime-impl'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'com.google.code.gson', name: 'gson'
}

test {
    useJUnitPlatform()
}

bootRun {
    systemProperties System.properties
}

bootJar {
    baseName = 'serve'
}

rootProject.distributionZip {
    dependsOn bootJar
    into(this.project.path.replace(":", "/")) {
        from bootJar.archivePath
    }
}

// Docker image configuration
jib {
    from {
        image = 'openjdk:8-jre-alpine'
    }
    to {
        image = dockerRepositoryPrefix + 'sagemaker-hosted-scorer'
        tags = [version]
    }
    container {
        ports = ['8080']
        volumes = [
                // mojo pipeline and license file will live here
                '/opt/ml/model'
        ]
        environment = [
                // location of driverless ai license file
                DRIVERLESS_AI_LICENSE_FILE: '/opt/ml/model/license.sig'
        ]
    }
}

tasks.build.dependsOn tasks.jibBuildTar